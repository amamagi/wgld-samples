<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
		<title>wgld.org WebGL sample 056</title>
		<script src="minMatrixb.js" type="text/javascript"></script>
		<script src="script.js" type="text/javascript"></script>
		
		<script id="vs" type="x-shader/x-vertex">
// グローシェーディング
attribute vec3 position;
attribute vec3 normal;
attribute vec4 color;
uniform   mat4 mvpMatrix;
uniform   mat4 invMatrix;
uniform   vec3 lightDirection;
uniform   vec3 eyeDirection;
uniform   vec4 ambientColor;
varying   vec4 vColor;

void main(void){
	vec3  invLight = normalize(invMatrix * vec4(lightDirection, 0.0)).xyz;
	vec3  invEye   = normalize(invMatrix * vec4(eyeDirection, 0.0)).xyz;
	vec3  halfLE   = normalize(invLight + invEye);
	float diffuse  = clamp(dot(normal, invLight), 0.0, 1.0);
	float specular = pow(clamp(dot(normal, halfLE), 0.0, 1.0), 50.0);
	vec4  amb      = color * ambientColor;
	vColor         = amb * vec4(vec3(diffuse), 1.0) + vec4(vec3(specular), 1.0);
	gl_Position    = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="fs" type="x-shader/x-fragment">
precision mediump float;

varying vec4 vColor;

void main(void){
	gl_FragColor = vColor;
}
		</script>
		
		<script id="ovs" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec2 texCoord;
uniform   mat4 mvpMatrix;
varying   vec2 vTexCoord;

void main(void){
	vTexCoord   = texCoord;
	gl_Position = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="ofs" type="x-shader/x-fragment">
// ズームブラー
precision mediump float;

uniform sampler2D texture;

// ブラーの強さ
// 大きいほど遠くのピクセルを参照し、強いブラーとなる
uniform float     strength;
varying vec2      vTexCoord;

const float tFrag = 1.0 / 512.0;
const float nFrag = 1.0 / 30.0;
const vec2  centerOffset = vec2(256.0, 256.0);

// 簡易乱数生成器 (0～1)
float rnd(vec3 scale, float seed){
	return fract(sin(dot(gl_FragCoord.stp + seed, scale)) * 43758.5453 + seed);
}

void main(void){
	vec3  destColor = vec3(0.0);
	float random = rnd(vec3(12.9898, 78.233, 151.7182), 0.0);

	// 画面座標をテクスチャ座標系に変換
	vec2  fc = vec2(gl_FragCoord.s, 512.0 - gl_FragCoord.t);

	// 中心から現在のピクセルへのベクトル
	vec2  fcc = fc - centerOffset;
	float totalWeight = 0.0;
	
	// 色の合成を30回行う
	for(float i = 0.0; i < 30.0; i++){

		// percentは0~1でiに比例
		float percent = (i + random) * nFrag;

		// 上に凸の二次関数を使ってガウシアンっぽいウェイトを得る
		float weight = percent - percent * percent;

		// 内側のピクセル座標を決定
		vec2  t = fc - fcc * percent * strength * nFrag;

		// 内側の色を参照
		destColor += texture2D(texture, t * tFrag).rgb * weight;
		
		// ウェイトをすべて足しても1にならないのでsumとっておく
		totalWeight += weight;
	}
	gl_FragColor = vec4(destColor / totalWeight, 1.0);
}
		</script>
	</head>
	<body>
		<canvas id="canvas"></canvas>
		<p>
			<input type="range" id="range" min="0" max="100" value="50"> strength
		</p>
		<p>
			<select id="select">
				<option value="render">render</option>
				<option value="texture1">texture 1</option>
				<option value="texture2">texture 2</option>
			</select>
		</p>
	</body>
</html>