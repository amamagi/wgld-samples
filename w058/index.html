<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
		<title>wgld.org WebGL sample 045</title>
		<script src="https://wgld.org/j/minMatrixb.js" type="text/javascript"></script>
		<script src="script.js" type="text/javascript"></script>
		
		<script id="vs" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec3 normal;
attribute vec4 color;
uniform   mat4 mvpMatrix;
uniform   mat4 invMatrix;
uniform   vec3 lightDirection;
uniform   vec3 eyeDirection;
uniform   vec4 ambientColor;
varying   vec4 vColor;

void main(void){
	vec3  invLight = normalize(invMatrix * vec4(lightDirection, 0.0)).xyz;
	vec3  invEye   = normalize(invMatrix * vec4(eyeDirection, 0.0)).xyz;
	vec3  halfLE   = normalize(invLight + invEye);
	float diffuse  = clamp(dot(normal, invLight), 0.1, 1.0);
	float specular = pow(clamp(dot(normal, halfLE), 0.0, 1.0), 40.0);
	vec4  amb      = color * ambientColor;
	vColor         = amb * vec4(vec3(diffuse), 1.0) + vec4(vec3(specular), 1.0);
	gl_Position    = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="fs" type="x-shader/x-fragment">
precision mediump float;

varying vec4 vColor;

void main(void){
	gl_FragColor = vColor;
}
		</script>
		
		<script id="svs" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec3 normal;
attribute vec4 color;
uniform   mat4 mvpMatrix;
uniform   mat4 invMatrix;
uniform   vec3 lightDirection;
uniform   vec3 eyeDirection;
varying   vec4 vColor;

void main(void){
	vec3  invLight = normalize(invMatrix * vec4(lightDirection, 0.0)).xyz;
	vec3  invEye   = normalize(invMatrix * vec4(eyeDirection, 0.0)).xyz;
	vec3  halfLE   = normalize(invLight + invEye);
	float specular = pow(clamp(dot(normal, halfLE), 0.0, 1.0), 50.0);
	vColor         = color * vec4(vec3(specular), 1.0);
	gl_Position    = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="sfs" type="x-shader/x-fragment">
precision mediump float;

varying vec4 vColor;

void main(void){
	gl_FragColor = vColor;
}
		</script>
		
		<script id="bvs" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec2 texCoord;
uniform   mat4 mvpMatrix;
varying   vec2 vTexCoord;

void main(void){
	vTexCoord   = texCoord;
	gl_Position = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="bfs" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D texture;
uniform bool      gaussian;
uniform float     weight[20];
uniform bool      horizontal;
varying vec2      vTexCoord;

void main(void){
	float tFrag = 1.0 / 512.0;
	vec2  fc;
	vec3  destColor = vec3(0.0);
	
	if(gaussian){
		if(horizontal){
			fc = vec2(gl_FragCoord.s, 512.0 - gl_FragCoord.t);
			for(int i=0; i < 20; i++){
				destColor += texture2D(texture, (fc + vec2(i, 0.0)) * tFrag).rgb * weight[i];
				if(i == 0) continue;
				destColor += texture2D(texture, (fc + vec2(-i, 0.0)) * tFrag).rgb * weight[i];
			}
		}else{
			fc = gl_FragCoord.st;
			for(int i=0; i < 20; i++){
				destColor += texture2D(texture, (fc + vec2(0.0, i)) * tFrag).rgb * weight[i];
				if(i == 0) continue;
				destColor += texture2D(texture, (fc + vec2(0.0, -i)) * tFrag).rgb * weight[i];
			}
		}
	}else{
		destColor = texture2D(texture, vTexCoord).rgb;
	}
	
	gl_FragColor = vec4(destColor, 1.0);
}
		</script>
		
		<script id="fvs" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec2 texCoord;
uniform   mat4 mvpMatrix;
varying   vec2 vTexCoord;

void main(void){
	vTexCoord   = texCoord;
	gl_Position = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="ffs" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D texture1;
uniform sampler2D texture2;
uniform bool      glare;
varying vec2      vTexCoord;

void main(void){
	vec4  destColor = texture2D(texture1, vTexCoord);
	vec4  smpColor  = texture2D(texture2, vec2(vTexCoord.s, 1.0 - vTexCoord.t));
	if(glare){
		destColor += smpColor * 2.0;
	}
	gl_FragColor = destColor;
}
		</script>
	</head>
	<body>
		<canvas id="canvas"></canvas>
		<p>
			<input id="check" type="checkbox">glare
		</p>
		<p>
			<input id="range" type="range" value="50" min="10" max="500">dispersion (1.0 to 50.0)
		</p>
	</body>
</html>