<html><head><meta http-equiv="Content-Type" content="text/html; charset=UTF-8"><!-- fragment shader --><script id="fs" type="x-shader/x-fragment">precision mediump float;
    uniform float time;
    uniform vec2  mouse;
    uniform vec2  resolution;
    
    // パーリンノイズを生成する

    const int   oct  = 8; // オクターブ。何階層のノイズを合成するか。
    const float per  = 0.5;// パーシステンス。各階層の合成の割合にこれの累乗を用いる。
    const float PI   = 3.1415926;
    const float cCorners = 1.0 / 16.0;
    const float cSides   = 1.0 / 8.0;
    const float cCenter  = 1.0 / 4.0;

    // 補間関数
    float interpolate(float a, float b, float x){
        float f = (1.0 - cos(x * PI)) * 0.5;
        return a * (1.0 - f) + b * f;
    }

    // 乱数生成
    float rnd(vec2 p){
        return fract(sin(dot(p ,vec2(12.9898,78.233))) * 43758.5453);
    }

    // 補間乱数
    float irnd(vec2 p){
        vec2 i = floor(p);
        vec2 f = fract(p);
        // 1x1四方の4角の乱数値を生成し、それらを用いてpの値を線形補間で導く
        vec4 v = vec4(rnd(vec2(i.x,       i.y      )),
                    rnd(vec2(i.x + 1.0, i.y      )),
                    rnd(vec2(i.x,       i.y + 1.0)),
                    rnd(vec2(i.x + 1.0, i.y + 1.0)));
        // 上辺とか下辺それぞれX方向で補間した値を得てからY軸で補間
        return interpolate(interpolate(v.x, v.y, f.x), interpolate(v.z, v.w, f.x), f.y);
    }

    // ノイズ生成
    float noise(vec2 p){ // p: 0~inf
        float t = 0.0;// 各階層のノイズを合計
        for(int i = 0; i < oct; i++){
            float freq = pow(2.0, float(i));// ノイズの解像度。i=0のとき最大(スクリーンと同じ)。
            float amp  = pow(per, float(oct - i)); // 階層が深いほど係数が指数的に小さくなる
            t += irnd(vec2(p.x / freq, p.y / freq)) * amp;
        }
        return t;
    }

    // シームレスノイズ生成
    float snoise(vec2 p, vec2 q, vec2 r){
        return noise(vec2(p.x,       p.y      )) *        q.x  *        q.y  +
            noise(vec2(p.x,       p.y + r.y)) *        q.x  * (1.0 - q.y) +
            noise(vec2(p.x + r.x, p.y      )) * (1.0 - q.x) *        q.y  +
            noise(vec2(p.x + r.x, p.y + r.y)) * (1.0 - q.x) * (1.0 - q.y);
    }

    void main(void){
        // noise
        vec2 t = gl_FragCoord.xy + vec2(time * 10.0);
        float n = noise(t);
        
        // seamless noise
    //	const float map = 256.0;
    //	vec2 t = mod(gl_FragCoord.xy + vec2(time * 10.0), map);
    //	float n = snoise(t, t / map, vec2(map));
        
        gl_FragColor = vec4(vec3(n), 1.0);
    }
    </script><!-- /fragment shader -->
    
    <!-- vertex shader --><script id="vs" type="x-shader/x-vertex">
    attribute vec3 position;
    
    void main(void){
        gl_Position = vec4(position, 1.0);
    }
    </script><!-- /vertex shader -->
    
    <script src="script.js" type="text/javascript"></script>
    <style type="text/css">
        * {
            text-align: center;
            margin: 10px auto;
            padding: 0px;
        }
        canvas {
            box-shadow: 0px 0px 0px 1px gray;
        }
    </style>
    <title>wgld.org WebGL sample 068</title>
    </head>
    <body>
        <canvas id="canvas"></canvas>
        <p><input type="checkbox" id="check" checked><label for="check"> auto run</label></p>
    </body></html>
    