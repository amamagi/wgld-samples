<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
		<title>wgld.org WebGL sample 040</title>
		<script src="minMatrixb.js" type="text/javascript"></script>
		<script src="script.js" type="text/javascript"></script>
		
		<script id="vs" type="x-shader/x-vertex">
// グローシェーディング
attribute vec3 position;
attribute vec3 normal;
attribute vec4 color;
uniform   mat4 mvpMatrix;
uniform   mat4 invMatrix;
uniform   vec3 lightDirection;
uniform   vec3 eyeDirection;
uniform   vec4 ambientColor;
varying   vec4 vColor;

void main(void){
	vec3  invLight = normalize(invMatrix * vec4(lightDirection, 0.0)).xyz;
	vec3  invEye   = normalize(invMatrix * vec4(eyeDirection, 0.0)).xyz;
	vec3  halfLE   = normalize(invLight + invEye);
	float diffuse  = clamp(dot(normal, invLight), 0.0, 1.0);
	float specular = pow(clamp(dot(normal, halfLE), 0.0, 1.0), 50.0);
	vec4  amb      = color * ambientColor;
	vColor         = amb * vec4(vec3(diffuse), 1.0) + vec4(vec3(specular), 1.0);
	gl_Position    = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="fs" type="x-shader/x-fragment">
precision mediump float;

varying vec4 vColor;

void main(void){
	gl_FragColor = vColor;
}
		</script>

		<script id="ovs" type="x-shader/x-vertex">
// イメージエフェクト用シェーダー
attribute vec3 position;
attribute vec2 texCoord;
uniform   mat4 mvpMatrix;
varying   vec2 vTexCoord;

void main(void){
	vTexCoord   = texCoord;
	gl_Position = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="ofs" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D texture;
uniform bool      grayScale;
varying vec2      vTexCoord;

// NTSC系加重平均法 (よくあるグレースケールの係数)
const float redScale   = 0.298912;
const float greenScale = 0.586611;
const float blueScale  = 0.114478;
const vec3  monochromeScale = vec3(redScale, greenScale, blueScale);

void main(void){
	vec4 smpColor = texture2D(texture, vTexCoord);
	if(grayScale){
		// オフスクリーンレンダリングした画像に係数をかけて足す
		float grayColor = dot(smpColor.rgb, monochromeScale);
		smpColor = vec4(vec3(grayColor), 1.0);
	}
	gl_FragColor = smpColor;
}
		</script>
	</head>
	<body>
		<canvas id="canvas"></canvas>
		<p>
			<input id="grayScale" name="radio" type="checkbox"> gray scale
		</p>
	</body>
</html>