<html><head><meta http-equiv="Content-Type" content="text/html; charset=UTF-8"><!-- fragment shader --><script id="fs" type="x-shader/x-fragment">precision mediump float;
    uniform float time;
    uniform vec2 mouse;
    uniform vec2 resolution;
    
    const vec3 cPos = vec3(0.0, 5.0, 5.0);
    const vec3 cDir = vec3(0.0, -0.707, -0.707);
    const vec3 cUp  = vec3(0.0,  0.707, -0.707);
    
    const vec3 lightDir = vec3(0.0, 1.0, 0.0);
    
    // torus distance function
    float distFuncTorus(vec3 p){
        p.xz -= mouse * 2.0 - 1.0;
        vec2 t = vec2(3.0, 1.0);
        vec2 r = vec2(length(p.xz) - t.x, p.y);
        return length(r) - t.y;
    }
    
    // floor distance function
    float distFuncFloor(vec3 p){
        return dot(p, vec3(0.0, 1.0, 0.0)) + 4.0;
    }
    
    // distance function
    float distFunc(vec3 p){
        float d1 = distFuncTorus(p);
        float d2 = distFuncFloor(p);
        return min(d1, d2);
    }
    
    // generate normal
    vec3 genNormal(vec3 p){
        float d = 0.0001;
        return normalize(vec3(
            distFunc(p + vec3(  d, 0.0, 0.0)) - distFunc(p + vec3( -d, 0.0, 0.0)),
            distFunc(p + vec3(0.0,   d, 0.0)) - distFunc(p + vec3(0.0,  -d, 0.0)),
            distFunc(p + vec3(0.0, 0.0,   d)) - distFunc(p + vec3(0.0, 0.0,  -d))
        ));
    }
    
    // generate shadow
    float genShadow(vec3 rayOrigin, vec3 rayDirection){
        float dist = 5.0;
        float totalDist = 0.001;
        float softCoef = 1.0;
        float shadowCoef = 0.5;
        float bokeh = 10.;
        for(float t = 0.0; t < 50.0; t++){
            dist = distFunc(rayOrigin + rayDirection * totalDist);
            if(dist < 0.001){
                // 最も濃い影
                return shadowCoef;
            }
            // オブジェクトを近傍を通るか、全くかすりもしない場合に小さくなる(？)
            softCoef = min(softCoef, bokeh * dist / totalDist);
            totalDist += dist;
        }
        return 1.0 - shadowCoef + softCoef * shadowCoef;
    }
    
    void main(void){
        // fragment position
        vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);
        
        // camera and ray
        vec3 cSide = cross(cDir, cUp);
        float targetDepth = 1.0;
        vec3 ray = normalize(cSide * p.x + cUp * p.y + cDir * targetDepth);
        
        // marching loop
        float tmp, dist;
        tmp = 0.0;
        vec3 dPos = cPos;
        for(int i = 0; i < 256; i++){
            dist = distFunc(dPos);
            if(dist < 0.001){break;}
            tmp += dist;
            dPos = cPos + tmp * ray;
        }
        
        // light offset
        vec3 light = normalize(lightDir + vec3(sin(time), 0.0, 0.0));
        
        // hit check
        vec3 color;
        float shadow = 1.0;
        if(abs(dist) < 0.001){
            // generate normal
            vec3 normal = genNormal(dPos);
            
            // light
            vec3 halfLE = normalize(light - ray);
            float diff = clamp(dot(light, normal), 0.1, 1.0);
            float spec = pow(clamp(dot(halfLE, normal), 0.0, 1.0), 50.0);
            
            // generate shadow
            shadow = genShadow(dPos + normal * 0.001, light);
            
            // generate tile pattern
            float u = 1.0 - floor(mod(dPos.x, 2.0));
            float v = 1.0 - floor(mod(dPos.z, 2.0));
            if((u == 1.0 && v < 1.0) || (u < 1.0 && v == 1.0)){
                diff *= 0.7;
            }
            
            color = vec3(1.0, 1.0, 1.0) * diff + vec3(spec);
        }else{
            color = vec3(0.0);
        }
        gl_FragColor = vec4(color * max(0.5, shadow), 1.0);
    }
    </script><!-- /fragment shader -->
    
    <!-- vertex shader --><script id="vs" type="x-shader/x-vertex">
    attribute vec3 position;
    
    void main(void){
        gl_Position = vec4(position, 1.0);
    }
    </script><!-- /vertex shader -->
    
    <script src="script.js" type="text/javascript"></script>
    <style type="text/css">
        * {
            text-align: center;
            margin: 10px auto;
            padding: 0px;
        }
        canvas {
            box-shadow: 0px 0px 0px 1px gray;
        }
    </style>
    <title>wgld.org WebGL sample 089</title>
    </head>
    <body>
        <canvas id="canvas"></canvas>
        <p><input type="checkbox" id="check" checked><label for="check"> auto run</label></p>
    </body></html>
    