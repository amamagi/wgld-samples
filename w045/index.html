<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
		<title>wgld.org WebGL sample 032</title>
		<script src="https://wgld.org/j/minMatrixb.js" type="text/javascript"></script>
		<script src="script.js" type="text/javascript"></script>
		
		<script id="vs" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec3 normal;
attribute vec4 color;
attribute vec2 textureCoord;
uniform   mat4 mMatrix;
uniform   mat4 mvpMatrix;
varying   vec3 vPosition;
varying   vec4 vColor;
varying   vec2 vTextureCoord;

// モデル変換した法線と接線をフラグメントシェーダにわたす
varying   vec3 tNormal;
varying   vec3 tTangent;

void main(void){
	vPosition     = (mMatrix * vec4(position, 1.0)).xyz;
	vColor        = color;
	vTextureCoord = textureCoord;

	// 法線をモデル変換
	tNormal       = (mMatrix * vec4(normal, 0.0)).xyz;

	// 接線を計算
	tTangent      = cross(tNormal, vec3(0.0, 1.0, 0.0));
	gl_Position   = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="fs" type="x-shader/x-fragment">
precision mediump float;

uniform vec3        eyePosition;
uniform sampler2D   normalMap;
uniform samplerCube cubeTexture;
uniform bool        reflection;
varying vec3        vPosition;
varying vec4        vColor;
varying vec2        vTextureCoord;
varying vec3        tNormal;
varying vec3        tTangent;

void main(void){
	vec3 tBinormal = cross(tNormal, tTangent);

	// 接空間からモデル空間に変換する行列
	mat3 mView     = mat3(tTangent, tBinormal, tNormal);

	// バンプマップの値をモデル空間内の法線ベクトルに変換
	vec3 mNormal   = mView * (texture2D(normalMap, vTextureCoord) * 2.0 - 1.0).rgb;
	vec3 ref;
	if(reflection){
		ref        = reflect(vPosition - eyePosition, mNormal);
	}else{
		ref        = tNormal;
	}
	vec4 envColor  = textureCube(cubeTexture, ref);
	vec4 destColor = vColor * envColor;
	gl_FragColor   = destColor;
}
		</script>
	</head>
	<body>
		<canvas id="canvas"></canvas>
	</body>
</html>