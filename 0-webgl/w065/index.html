<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
		<title>wgld.org WebGL sample 054</title>
		<script src="minMatrixb.js" type="text/javascript"></script>
		<script src="script.js" type="text/javascript"></script>
		
		<script id="main_vs" type="x-shader/x-vertex">
// 最終シーンのレンダリング用
attribute vec3  position;
attribute vec3  normal;
attribute vec4  color;
uniform   mat4  mMatrix;
uniform   mat4  mvpMatrix;
uniform   mat4  invMatrix;
uniform   mat4  tMatrix;
uniform   vec3  lightPosition;
uniform   vec3  eyes;
uniform   vec3  eyePosition;
uniform   vec4  ambientColor;
varying   vec4  vColor;
varying   vec4  vTexCoord;
varying   float vDotLE;

void main(void){
	vec3  pos      = (mMatrix * vec4(position, 1.0)).xyz;
	vec3  invLight = normalize(invMatrix * vec4(lightPosition - pos, 0.0)).xyz;
	vec3  invEye   = normalize(invMatrix * vec4(eyePosition, 0.0)).xyz;
	vec3  halfLE   = normalize(invLight + invEye);
	float diffuse  = clamp(dot(normal, invLight), 0.0, 1.0);
	float specular = pow(clamp(dot(normal, halfLE), 0.0, 1.0), 50.0);
	vColor         = color * vec4(vec3(diffuse), 1.0) + vec4(vec3(specular), 0.0) + ambientColor;
	vTexCoord      = tMatrix * vec4(pos, 1.0);

	// マイナス視線ベクトルとライトベクトルの内積をpowでコントラスト強調した係数を計算
	vDotLE         = pow(max(dot(normalize(eyes - eyePosition), normalize(lightPosition)), 0.0), 10.0);
	gl_Position    = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="main_fs" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D blurTexture;
varying vec4      vColor;
varying vec4      vTexCoord;
varying float     vDotLE;

const vec3 throughColor = vec3(1.0, 0.5, 0.2);

void main(void){
	float bDepth  = pow(texture2DProj(blurTexture, vTexCoord).r, 15.0);
	vec3  through = throughColor * vDotLE * bDepth;
	gl_FragColor  = vec4(vColor.rgb + through, vColor.a);
}
		</script>
		
		<script id="difference_vs" type="x-shader/x-vertex">
// 深度の差分をとって厚みをレンダリング
attribute vec3  position;
uniform   mat4  mMatrix;
uniform   mat4  mvpMatrix;
uniform   mat4  tMatrix;
uniform   vec3  eyePosition;
varying   float vDepth;
varying   vec4  vTexCoord;

const float near = 0.1;
const float far  = 15.0;
const float linerDepth = 1.0 / (far - near);

void main(void){
	vec3 pos    = (mMatrix * vec4(position, 1.0)).xyz;
	vDepth      = length(eyePosition - pos) * linerDepth;
	vTexCoord   = tMatrix * vec4(pos, 1.0);
	gl_Position = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="difference_fs" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D backFaceTexture;
varying float     vDepth;
varying vec4      vTexCoord;

void main(void){
	float bDepth    = 1.0 - texture2DProj(backFaceTexture, vTexCoord).r;
	float differnce = 1.0 - clamp(bDepth - vDepth, 0.0, 1.0);
	gl_FragColor    = vec4(vec3(differnce), 1.0);
}
		</script>
		
		<script id="depth_vs" type="x-shader/x-vertex">
// 深度レンダリング
attribute vec3  position;
uniform   mat4  mMatrix;
uniform   mat4  mvpMatrix;
uniform   vec3  eyePosition;
varying   vec4  vColor;

const float near = 0.1;
const float far  = 15.0;
const float linerDepth = 1.0 / (far - near);

void main(void){
	vec3  pos   = (mMatrix * vec4(position, 1.0)).xyz;
	float depth = length(eyePosition - pos) * linerDepth;
	vColor      = vec4(vec3(depth), 1.0);
	gl_Position = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="point_vs" type="x-shader/x-vertex">
// 光源位置の描画用
attribute vec3 position;
uniform   mat4 mvpMatrix;
varying   vec4 vColor;

void main(void){
	vColor       = vec4(1.0, 1.0, 0.25, 1.0);
	gl_PointSize = 8.0;
	gl_Position  = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="gp_fs" type="x-shader/x-fragment">
precision mediump float;

varying vec4 vColor;

void main(void){
	gl_FragColor = vColor;
}
		</script>
		
		<script id="blur_vs" type="x-shader/x-vertex">
// ぼかしフィルター (深度値をぼかして散乱光を表現)
attribute vec3 position;
attribute vec2 texCoord;
uniform   mat4 ortMatrix;
varying   vec2 vTexCoord;

void main(void){
	vTexCoord   = texCoord;
	gl_Position = ortMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="blur_fs" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D texture;
uniform float     weight[5];
uniform bool      horizontal;
varying vec2      vTexCoord;

const float screenWidth = 512.0;
const float tFrag = 1.0 / screenWidth;

void main(void){
	vec2 fc = gl_FragCoord.st;
	vec3 destColor = vec3(0.0);
	if(horizontal){
		destColor += texture2D(texture, (fc + vec2(-4.0, 0.0)) * tFrag).rgb * weight[4];
		destColor += texture2D(texture, (fc + vec2(-3.0, 0.0)) * tFrag).rgb * weight[3];
		destColor += texture2D(texture, (fc + vec2(-2.0, 0.0)) * tFrag).rgb * weight[2];
		destColor += texture2D(texture, (fc + vec2(-1.0, 0.0)) * tFrag).rgb * weight[1];
		destColor += texture2D(texture, (fc + vec2( 0.0, 0.0)) * tFrag).rgb * weight[0];
		destColor += texture2D(texture, (fc + vec2( 1.0, 0.0)) * tFrag).rgb * weight[1];
		destColor += texture2D(texture, (fc + vec2( 2.0, 0.0)) * tFrag).rgb * weight[2];
		destColor += texture2D(texture, (fc + vec2( 3.0, 0.0)) * tFrag).rgb * weight[3];
		destColor += texture2D(texture, (fc + vec2( 4.0, 0.0)) * tFrag).rgb * weight[4];
	}else{
		destColor += texture2D(texture, (fc + vec2(0.0, -4.0)) * tFrag).rgb * weight[4];
		destColor += texture2D(texture, (fc + vec2(0.0, -3.0)) * tFrag).rgb * weight[3];
		destColor += texture2D(texture, (fc + vec2(0.0, -2.0)) * tFrag).rgb * weight[2];
		destColor += texture2D(texture, (fc + vec2(0.0, -1.0)) * tFrag).rgb * weight[1];
		destColor += texture2D(texture, (fc + vec2(0.0,  0.0)) * tFrag).rgb * weight[0];
		destColor += texture2D(texture, (fc + vec2(0.0,  1.0)) * tFrag).rgb * weight[1];
		destColor += texture2D(texture, (fc + vec2(0.0,  2.0)) * tFrag).rgb * weight[2];
		destColor += texture2D(texture, (fc + vec2(0.0,  3.0)) * tFrag).rgb * weight[3];
		destColor += texture2D(texture, (fc + vec2(0.0,  4.0)) * tFrag).rgb * weight[4];
	}
	gl_FragColor = vec4(vec3(1.0) - destColor, 1.0);
}
		</script>
		
		<script id="ortho_vs" type="x-shader/x-vertex">
// バッファのプレビュー用
attribute vec3 position;
attribute vec2 texCoord;
uniform   mat4 mvpMatrix;
varying   vec2 vTexCoord;

void main(void){
	vTexCoord   = texCoord;
	gl_Position = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="ortho_fs" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D texture;
varying vec2      vTexCoord;

void main(void){
	gl_FragColor = texture2D(texture, vTexCoord);
}
		</script>
	</head>
	<body>
		<canvas id="canvas"></canvas>
	</body>
</html>