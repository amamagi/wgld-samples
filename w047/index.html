<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
		<title>wgld.org WebGL sample 034</title>
		<script src="https://wgld.org/j/minMatrixb.js" type="text/javascript"></script>
		<script src="script.js" type="text/javascript"></script>
		
		<script id="svs" type="x-shader/x-vertex">
// キューブマップにレンダリングするオブジェクトに適用するシェーダー
// 頂点シェーダーでライティングを行うことで軽量化している
attribute vec3 position;
attribute vec3 normal;
attribute vec4 color;
uniform   mat4 mvpMatrix;
uniform   mat4 invMatrix;
uniform   vec3 lightDirection;
uniform   vec3 eyeDirection;
uniform   vec4 ambientColor;
varying   vec4 vColor;

void main(void){
	vec3  invLight = normalize(invMatrix * vec4(lightDirection, 0.0)).xyz;
	vec3  invEye   = normalize(invMatrix * vec4(eyeDirection, 0.0)).xyz;
	vec3  halfLE   = normalize(invLight + invEye);
	float diffuse  = clamp(dot(normal, invLight), 0.0, 1.0);
	float specular = pow(clamp(dot(normal, halfLE), 0.0, 1.0), 50.0);
	vec4  amb      = color * ambientColor;
	vColor         = amb * vec4(vec3(diffuse), 1.0) + vec4(vec3(specular), 1.0);
	gl_Position    = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="sfs" type="x-shader/x-fragment">
// キューブマップにレンダリングするオブジェクトに適用するシェーダー
// 頂点シェーダーでライティングを行うことで軽量化している
precision mediump float;

varying vec4 vColor;

void main(void){
	gl_FragColor = vColor;
}
		</script>
		
		<script id="cvs" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec3 normal;
attribute vec4 color;
uniform   mat4 mMatrix;
uniform   mat4 mvpMatrix;
varying   vec3 vPosition;
varying   vec3 vNormal;
varying   vec4 vColor;

void main(void){
	vPosition   = (mMatrix * vec4(position, 1.0)).xyz;
	vNormal     = (mMatrix * vec4(normal, 0.0)).xyz;
	vColor      = color;
	gl_Position = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="cfs" type="x-shader/x-fragment">
precision mediump float;

uniform vec3        eyePosition;
uniform samplerCube cubeTexture;
uniform bool        reflection;
varying vec3        vPosition;
varying vec3        vNormal;
varying vec4        vColor;

void main(void){
	vec3 ref;
	if(reflection){
		ref = reflect(vPosition - eyePosition, vNormal);
	}else{
		ref = vNormal;
	}
	vec4 envColor  = textureCube(cubeTexture, ref);
	vec4 destColor = vColor * envColor;
	gl_FragColor   = destColor;
}
		</script>
	</head>
	<body>
		<canvas id="canvas"></canvas>
	</body>
</html>