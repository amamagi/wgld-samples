<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
		<title>wgld.org WebGL sample 059</title>
		<script src="minMatrixb.js" type="text/javascript"></script>
		<script src="script.js" type="text/javascript"></script>
		
		<script id="dvs" type="x-shader/x-vertex">
// 深度格納用シェーダー
attribute vec3 position;
uniform mat4 mvpMatrix;
varying vec4 vPosition;

void main(void){
	vPosition = mvpMatrix * vec4(position, 1.0);
	gl_Position = vPosition;
}
		</script>
		
		<script id="dfs" type="x-shader/x-fragment">
precision mediump float;

varying vec4 vPosition;

void main(void){
	// クリップ空間の大きさwを使って-11正規化してから01正規化
	float depth = (vPosition.z / vPosition.w + 1.0) * 0.5;
	gl_FragColor = vec4(vec3(depth), 1.0);
}
		</script>
		
		<script id="svs" type="x-shader/x-vertex">
// シャドウマッピング
attribute vec3 position;
attribute vec3 normal;
attribute vec4 color;
uniform   mat4 mMatrix;
uniform   mat4 mvpMatrix;
uniform   mat4 tMatrix;
uniform   mat4 lgtMatrix;
varying   vec3 vPosition;
varying   vec3 vNormal;
varying   vec4 vColor;
varying   vec4 vTexCoord;
varying   vec4 vDepth;

void main(void){
	vPosition   = (mMatrix * vec4(position, 1.0)).xyz;
	vNormal     = normal;
	vColor      = color;
	vTexCoord   = tMatrix * vec4(vPosition, 1.0);
	vDepth      = lgtMatrix * vec4(position, 1.0);
	gl_Position = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="sfs" type="x-shader/x-fragment">
precision mediump float;

uniform mat4      invMatrix;
uniform vec3      lightPosition;
uniform sampler2D texture;
varying vec3      vPosition;
varying vec3      vNormal;
varying vec4      vColor;
varying vec4      vTexCoord;
varying vec4      vDepth;

void main(void){
	float lightCoord = 0.0;
	vec3  light     = lightPosition - vPosition;
	vec3  invLight  = normalize(invMatrix * vec4(light, 0.0)).xyz;
	float diffuse   = clamp(dot(vNormal, invLight), 0.2, 1.0);
	// 今回は深度値を保存するのは1チャンネルのみ
	float shadow    = texture2DProj(texture, vTexCoord).r;
	vec4 depthColor = vec4(1.0);
	if(vDepth.w > 0.0){
		// 頂点深度を01正規化
		lightCoord = (vDepth.z / vDepth.w + 1.0) * 0.5;
		if((lightCoord - 0.01) > shadow){
			depthColor = vec4(0.5, 0.5, 0.5, 1.0);
		}
	}
	gl_FragColor = vColor * vec4(vec3(diffuse), 1.0) * depthColor;
}
		</script>
	</head>
	<body>
		<canvas id="canvas"></canvas>
		<p>
			<input id="range" type="range" max="50" min="40" value="45"> light scale
		</p>
	</body>
</html>