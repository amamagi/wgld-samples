<html><head><meta http-equiv="Content-Type" content="text/html; charset=UTF-8"><!-- fragment shader --><script id="fs" type="x-shader/x-fragment">precision mediump float;
    uniform float time;
    uniform vec2  mouse;
    uniform vec2  resolution;
    
    // HSV カラー生成関数
    vec3 hsv(float h, float s, float v){
        vec4 t = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
        vec3 p = abs(fract(vec3(h) + t.xyz) * 6.0 - vec3(t.w));
        return v * mix(vec3(t.x), clamp(p - vec3(t.x), 0.0, 1.0), s);
    }
    
    void main(void){
        // マウス座標の正規化
        vec2 m = vec2(mouse.x * 2.0 - 1.0, -mouse.y * 2.0 + 1.0);
        
        // フラグメント座標の正規化
        vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);
        
        // マンデルブロ集合
        int j = 0;                     // カウンタ
        vec2  c = p + vec2(-0.5, 0.0); // 原点を少しずらす
        //float magnify = 1.5 - mouse.x * 0.5; // マウス座標を使って拡大度を変更
        vec2  z = vec2(0.0, 0.0);      // 漸化式 Z の初期値
        
        // 漸化式の繰り返し処理(今回は 360 回ループ)
        for(int i = 0; i < 360; i++){
            j++;
            if(length(z) > 2.0){break;}

            // zとcは虚数
            // z^2 = (x + iy)^2 = (x^2 - y^2) + i(2xy)
            z = vec2(z.x * z.x - z.y * z.y, 2.0 * z.x * z.y) + c;// * magnify;
        }
        
        // 時間の経過で色を HSV 出力する
        float h = mod(time * 20.0, 360.0) / 360.0;
        vec3 rgb = hsv(h, 1.0, 1.0);
        
        // 漸化式で繰り返した回数をもとに輝度を決める
        float t = float(j) / 360.0;
        
        // 最終的な色の出力
        gl_FragColor = vec4(rgb * t, 1.0);
        
    }
    </script><!-- /fragment shader -->
    
    <!-- vertex shader --><script id="vs" type="x-shader/x-vertex">
    attribute vec3 position;
    
    void main(void){
        gl_Position = vec4(position, 1.0);
    }
    </script><!-- /vertex shader -->
    
    <script src="script.js" type="text/javascript"></script>
    <style type="text/css">
        * {
            text-align: center;
            margin: 10px auto;
            padding: 0px;
        }
        canvas {
            box-shadow: 0px 0px 0px 1px gray;
        }
    </style>
    <title>wgld.org WebGL sample 068</title>
    </head>
    <body>
        <canvas id="canvas"></canvas>
        <p><input type="checkbox" id="check" checked><label for="check"> auto run</label></p>
    </body></html>
    