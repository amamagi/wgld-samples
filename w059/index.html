<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
		<title>wgld.org WebGL sample 046</title>
		<script src="https://wgld.org/j/minMatrixb.js" type="text/javascript"></script>
		<script src="script.js" type="text/javascript"></script>
		
		<script id="main_vs" type="x-shader/x-vertex">
// グローシェーディング
attribute vec3 position;
attribute vec3 normal;
attribute vec4 color;
attribute vec2 texCoord;
uniform   mat4 mvpMatrix;
uniform   mat4 invMatrix;
uniform   vec3 lightDirection;
uniform   vec3 eyeDirection;
uniform   vec4 ambientColor;
varying   vec4 vColor;
varying   vec2 vTexCoord;

void main(void){
	vec3  invLight = normalize(invMatrix * vec4(lightDirection, 0.0)).xyz;
	vec3  invEye   = normalize(invMatrix * vec4(eyeDirection, 0.0)).xyz;
	vec3  halfLE   = normalize(invLight + invEye);
	float diffuse  = clamp(dot(normal, invLight), 0.1, 1.0);
	float specular = pow(clamp(dot(normal, halfLE), 0.0, 1.0), 50.0);
	vec4  amb      = color * ambientColor;
	vColor         = amb * vec4(vec3(diffuse), 1.0) + vec4(vec3(specular), 1.0);
	vTexCoord      = texCoord;
	gl_Position    = mvpMatrix * vec4(position, 1.0);
}
		</script>

		<script id="main_fs" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D texture;
varying vec4      vColor;
varying vec2      vTexCoord;

void main(void){
	gl_FragColor = vColor * texture2D(texture, vTexCoord);
}
		</script>
			
		<script id="depthMap_vs" type="x-shader/x-vertex">
// フォーカスが合う領域が1となる深度マップ	
attribute vec3 position;
uniform   mat4 mvpMatrix;

varying   vec4 vPosition;

void main(void){
	vPosition   = mvpMatrix * vec4(position, 1.0);
	gl_Position = vPosition;
}
		</script>

		<script id="depthMap_fs" type="x-shader/x-fragment">
precision mediump float;

uniform float depthOffset;
varying vec4  vPosition;

const float near = 0.1;
const float far  = 30.0;
const float linerDepth = 1.0 / (far - near);

vec4 convRGBA(float depth){
	float r = depth;
	float g = fract(r * 255.0);
	float b = fract(g * 255.0);
	float a = fract(b * 255.0);
	float coef = 1.0 / 255.0;
	r -= g * coef;
	g -= b * coef;
	b -= a * coef;
	return vec4(r, g, b, a);
}
// ピントが合う領域を1にする
float convCoord(float depth, float offset){
	float d = clamp(depth + offset, 0.0, 1.0);
	if(d > 0.6){
		d = 2.5 * (1.0 - d);
	}else if(d >= 0.4){
		d = 1.0;
	}else{
		d *= 2.5;
	}
	return d;
}

void main(void){
	float liner     = linerDepth * length(vPosition);
	vec4  convColor = convRGBA(convCoord(liner, depthOffset));
	gl_FragColor    = convColor;
}
		</script>
		
		<script id="gaussian_vs" type="x-shader/x-vertex">
// gaussian filter
attribute vec3 position;
attribute vec2 texCoord;
uniform   mat4 mvpMatrix;
varying   vec2 vTexCoord;

void main(void){
	vTexCoord   = texCoord;
	gl_Position = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="gaussian_fs" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D texture;
uniform bool      gaussian;
uniform bool      horizontal;
uniform float     bufferWidth;
uniform float     weight[10];
varying vec2      vTexCoord;

void main(void){
	float tFrag = 1.0 / bufferWidth;
	vec2  fc;
	vec3  destColor = vec3(0.0);
	
	if(gaussian){
		if(horizontal){
			fc = vec2(gl_FragCoord.s, bufferWidth - gl_FragCoord.t);
			destColor += texture2D(texture, (fc + vec2(-9.0, 0.0)) * tFrag).rgb * weight[9];
			destColor += texture2D(texture, (fc + vec2(-8.0, 0.0)) * tFrag).rgb * weight[8];
			destColor += texture2D(texture, (fc + vec2(-7.0, 0.0)) * tFrag).rgb * weight[7];
			destColor += texture2D(texture, (fc + vec2(-6.0, 0.0)) * tFrag).rgb * weight[6];
			destColor += texture2D(texture, (fc + vec2(-5.0, 0.0)) * tFrag).rgb * weight[5];
			destColor += texture2D(texture, (fc + vec2(-4.0, 0.0)) * tFrag).rgb * weight[4];
			destColor += texture2D(texture, (fc + vec2(-3.0, 0.0)) * tFrag).rgb * weight[3];
			destColor += texture2D(texture, (fc + vec2(-2.0, 0.0)) * tFrag).rgb * weight[2];
			destColor += texture2D(texture, (fc + vec2(-1.0, 0.0)) * tFrag).rgb * weight[1];
			destColor += texture2D(texture, (fc + vec2( 0.0, 0.0)) * tFrag).rgb * weight[0];
			destColor += texture2D(texture, (fc + vec2( 1.0, 0.0)) * tFrag).rgb * weight[1];
			destColor += texture2D(texture, (fc + vec2( 2.0, 0.0)) * tFrag).rgb * weight[2];
			destColor += texture2D(texture, (fc + vec2( 3.0, 0.0)) * tFrag).rgb * weight[3];
			destColor += texture2D(texture, (fc + vec2( 4.0, 0.0)) * tFrag).rgb * weight[4];
			destColor += texture2D(texture, (fc + vec2( 5.0, 0.0)) * tFrag).rgb * weight[5];
			destColor += texture2D(texture, (fc + vec2( 6.0, 0.0)) * tFrag).rgb * weight[6];
			destColor += texture2D(texture, (fc + vec2( 7.0, 0.0)) * tFrag).rgb * weight[7];
			destColor += texture2D(texture, (fc + vec2( 8.0, 0.0)) * tFrag).rgb * weight[8];
			destColor += texture2D(texture, (fc + vec2( 9.0, 0.0)) * tFrag).rgb * weight[9];
		}else{
			fc = gl_FragCoord.st;
			destColor += texture2D(texture, (fc + vec2(0.0, -9.0)) * tFrag).rgb * weight[9];
			destColor += texture2D(texture, (fc + vec2(0.0, -8.0)) * tFrag).rgb * weight[8];
			destColor += texture2D(texture, (fc + vec2(0.0, -7.0)) * tFrag).rgb * weight[7];
			destColor += texture2D(texture, (fc + vec2(0.0, -6.0)) * tFrag).rgb * weight[6];
			destColor += texture2D(texture, (fc + vec2(0.0, -5.0)) * tFrag).rgb * weight[5];
			destColor += texture2D(texture, (fc + vec2(0.0, -4.0)) * tFrag).rgb * weight[4];
			destColor += texture2D(texture, (fc + vec2(0.0, -3.0)) * tFrag).rgb * weight[3];
			destColor += texture2D(texture, (fc + vec2(0.0, -2.0)) * tFrag).rgb * weight[2];
			destColor += texture2D(texture, (fc + vec2(0.0, -1.0)) * tFrag).rgb * weight[1];
			destColor += texture2D(texture, (fc + vec2(0.0,  0.0)) * tFrag).rgb * weight[0];
			destColor += texture2D(texture, (fc + vec2(0.0,  1.0)) * tFrag).rgb * weight[1];
			destColor += texture2D(texture, (fc + vec2(0.0,  2.0)) * tFrag).rgb * weight[2];
			destColor += texture2D(texture, (fc + vec2(0.0,  3.0)) * tFrag).rgb * weight[3];
			destColor += texture2D(texture, (fc + vec2(0.0,  4.0)) * tFrag).rgb * weight[4];
			destColor += texture2D(texture, (fc + vec2(0.0,  5.0)) * tFrag).rgb * weight[5];
			destColor += texture2D(texture, (fc + vec2(0.0,  6.0)) * tFrag).rgb * weight[6];
			destColor += texture2D(texture, (fc + vec2(0.0,  7.0)) * tFrag).rgb * weight[7];
			destColor += texture2D(texture, (fc + vec2(0.0,  8.0)) * tFrag).rgb * weight[8];
			destColor += texture2D(texture, (fc + vec2(0.0,  9.0)) * tFrag).rgb * weight[9];
		}
	}else{
		destColor = texture2D(texture, vTexCoord).rgb;
	}
	
	gl_FragColor = vec4(destColor, 1.0);
}
		</script>
		
		<script id="final_vs" type="x-shader/x-vertex">
// 普通にレンダリングしたテクスチャとブラーを掛けたテクスチャ2枚を深度マップ(深度ではない)を使って混ぜる
attribute vec3 position;
attribute vec2 texCoord;
uniform   mat4 mvpMatrix;
varying   vec2 vTexCoord;

void main(void){
	vTexCoord   = texCoord;
	gl_Position = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="final_fs" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D depthTexture;
uniform sampler2D sceneTexture;
uniform sampler2D blurTexture1;
uniform sampler2D blurTexture2;
uniform int       result;
varying vec2      vTexCoord;

float restDepth(vec4 RGBA){
	const float rMask = 1.0;
	const float gMask = 1.0 / 255.0;
	const float bMask = 1.0 / (255.0 * 255.0);
	const float aMask = 1.0 / (255.0 * 255.0 * 255.0);
	float depth = dot(RGBA, vec4(rMask, gMask, bMask, aMask));
	return depth;
}

void main(void){
	float d = restDepth(texture2D(depthTexture, vec2(vTexCoord.s, 1.0 - vTexCoord.t)));
	float coef = 1.0 - d;
	float blur1Coef = coef * d;
	float blur2Coef = coef * coef;
	vec4 sceneColor = texture2D(sceneTexture, vec2(vTexCoord.s, 1.0 - vTexCoord.t));
	vec4 blur1Color = texture2D(blurTexture1, vTexCoord);
	vec4 blur2Color = texture2D(blurTexture2, vTexCoord);
	// 深度に応じて3枚のテクスチャを混ぜる
	vec4 destColor  = sceneColor * d + blur1Color * blur1Coef + blur2Color * blur2Coef;
	if(result == 0){
		gl_FragColor = destColor;
	}else if(result == 1){
		gl_FragColor = vec4(vec3(d), 1.0);
	}else if(result == 2){
		gl_FragColor = sceneColor;
	}else if(result == 3){
		gl_FragColor = blur1Color;
	}else{
		gl_FragColor = blur2Color;
	}
}
		</script>
		
	</head>
	<body>
		<canvas id="canvas"></canvas>
		<p>
			<select id="select">
				<option >depth of field</option>
				<option >depth</option>
				<option >scene</option>
				<option >blur scene1</option>
				<option >blur scene2</option>
			</select>
		</p>
		<p>
			<input id="range" type="range" value="50" min="0" max="100">targetDepth
		</p>
	</body>
</html>