<html><head><meta http-equiv="Content-Type" content="text/html; charset=UTF-8"><!-- fragment shader --><script id="fs" type="x-shader/x-fragment">precision mediump float;
    uniform float time;
    uniform vec2  mouse;
    uniform vec2  resolution;
    const float PI = 3.14159265;
    const float angle = 60.0;
    const float fov = angle * 0.5 * PI / 180.0;

    vec3  cPos = vec3(0.0, 0.0, 2.0);
    const float sphereSize = 1.0;
    const vec3 lightDir = vec3(-0.577, 0.577, 0.577);

    vec3 trans(vec3 p){
        return mod(p, 4.0) - 2.0;
    }

    float distanceFunc(vec3 p){
        return length(trans(p)) - sphereSize;
    }

    vec3 getNormal(vec3 p){
        float d = 0.0001;
        return normalize(vec3(
            distanceFunc(p + vec3(  d, 0.0, 0.0)) - distanceFunc(p + vec3( -d, 0.0, 0.0)),
            distanceFunc(p + vec3(0.0,   d, 0.0)) - distanceFunc(p + vec3(0.0,  -d, 0.0)),
            distanceFunc(p + vec3(0.0, 0.0,   d)) - distanceFunc(p + vec3(0.0, 0.0,  -d))
        ));
    }

    void main(void){
        // fragment position
        vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);
        
        // ray
        vec3 ray = normalize(vec3(sin(fov) * p.x, sin(fov) * p.y, -cos(fov)));	
        
        // marching loop
        float distance = 0.0;
        float rLen = 0.0;
        vec3  rPos = cPos;
        for(int i = 0; i < 64; i++){
            distance = distanceFunc(rPos);
            rLen += distance;
            rPos = cPos + ray * rLen;
        }
        
        // hit check
        if(abs(distance) < 0.001){
            vec3 normal = getNormal(rPos);
            float diff = clamp(dot(lightDir, normal), 0.1, 1.0);
            gl_FragColor = vec4(vec3(diff), 1.0);
        }else{
            gl_FragColor = vec4(vec3(0.0), 1.0);
        }
    }
    </script><!-- /fragment shader -->
    
    <!-- vertex shader --><script id="vs" type="x-shader/x-vertex">
    attribute vec3 position;
    
    void main(void){
        gl_Position = vec4(position, 1.0);
    }
    </script><!-- /vertex shader -->
    
    <script src="script.js" type="text/javascript"></script>
    <style type="text/css">
        * {
            text-align: center;
            margin: 10px auto;
            padding: 0px;
        }
        canvas {
            box-shadow: 0px 0px 0px 1px gray;
        }
    </style>
    <title>wgld.org WebGL sample 068</title>
    </head>
    <body>
        <canvas id="canvas"></canvas>
        <p><input type="checkbox" id="check" checked><label for="check"> auto run</label></p>
    </body></html>
    