<html><head><meta name="viewport" content="width=device-width, initial-scale=1.0" /><!-- fragment shader --><script id="fs" type="x-shader/x-fragment">precision mediump float;
uniform float time;
uniform vec2  mouse;
uniform vec2  resolution;
const float PI = 3.14159265;
const float angle = 120.0;
const float fov = angle * 0.5 * PI / 180.0;

vec3  cPos = vec3(0.0, 0.0, -5.0);
const float sphereSize = 1.0;
vec3 lightDir = vec3(-0.577, 0.577, -0.577);

vec3 trans(vec3 p, float factor){
    float hFactor = factor / 2.;
    return mod(p + vec3(hFactor,hFactor,hFactor), factor) - hFactor;
}

float sdCone( vec3 p, vec2 c, float h )
{
  float q = length(p.xz);
  return max(dot(c.xy,vec2(q,p.y)),-h-p.y);
}

float sdSphere(vec3 p, float rad){
    return length(p) - rad;
}

float sdBox( vec3 p, vec3 b )
{
  vec3 q = abs(p) - b;
  return length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
}

float sdHemi(vec3 p, float h){
    float hemi = max( sdSphere(p, h),
                    -sdBox(p - vec3(0., h, 0.), vec3(.5, h, .5))
                );
    return hemi;
}

float sdTear(vec3 p){
    p = vec3(p.x, p.y + 0.5, p.z);
    if(p.y > 1.) return length(p - vec3(.0, 1., .0));
    if(p.y < 0.) return pow(length(p - vec3(.0, .75, .0)) - 0.1, 4.);
    float r = sin(PI * pow(clamp(p.y, 0., 1.), 0.55)) * 0.27;
    return length(p.xz) - r;
}

float opSmoothUnion( float d1, float d2, float k ) {
    float h = exp(-k * d1) + exp(-k * d2);
    return -log(h) / k;
}

float distanceFunc(vec3 p){
    vec3 q = vec3(p.x, p.y / .8, p.z);
    return mix(sdTear(p), sdTear(trans(p, 6.0)), (sin(time/7.) + 1.5) * 0.5);
}

vec3 getNormal(vec3 p){
    float d = 0.002;
    return normalize(vec3(
        distanceFunc(p + vec3(  d, 0.0, 0.0)) - distanceFunc(p + vec3( -d, 0.0, 0.0)),
        distanceFunc(p + vec3(0.0,   d, 0.0)) - distanceFunc(p + vec3(0.0,  -d, 0.0)),
        distanceFunc(p + vec3(0.0, 0.0,   d)) - distanceFunc(p + vec3(0.0, 0.0,  -d))
    ));
}

void main(void){
    vec2 p = (gl_FragCoord.xy * 2. - resolution) / max(resolution.x, resolution.y);
    vec2 m = vec2((mouse.x - 0.5) * 1.3, -(mouse.y - 0.5) * 1.3);

    lightDir = vec3(m.x, m.y, -1.);
    vec3 ray = vec3(sin(fov) * p.x, sin(fov) * p.y, cos(fov));
    //ray -= vec3(m, 0.) * .5;
    //cPos += vec3(m, 0.) * .5;
    
    // marching loop
    float dist = .0;
    float rLen = .0;
    vec3 rPos = cPos;
    float shadow = 1.0;
    for(int i=0;i<60;i++){
        dist = distanceFunc(rPos);
        rLen += dist;
        rPos = cPos + ray * rLen;

        shadow = min(shadow, dist * 16.0 / rLen);
    }

    vec3 color;
    if(abs(dist) < 0.001){
        vec3 nor = getNormal(rPos);
        float diffuse = clamp(dot(lightDir, nor), 0.1, 1.);
        float rim = pow(1.-dot(vec3(.0, .0, -1.), nor), 1.);
        color = vec3(1., 1., 1.) * diffuse * 0.5 + vec3(1., 1., 1.) * rim * 0.7;
        //color = (nor+1.)/2.;
    }else{
        color = vec3(.85, .85, .85) * (1. -pow((1. - shadow), 5.0) / 3.);
    }
     
    color += vec3(1., 1., 1.) * (1. -pow((1. - shadow), 5.0) / 3.) * 0.3;
    gl_FragColor = vec4(color, 1.0);

}
</script><!-- /fragment shader -->

<!-- vertex shader --><script id="vs" type="x-shader/x-vertex">
attribute vec3 position;

void main(void){
    gl_Position = vec4(position, 1.0);
}
</script><!-- /vertex shader -->
<script src="script.js" type="text/javascript"></script>
<style type="text/css">
html {
    overflow: hidden;
    position: fixed;
    touch-action: none;
}
body {
    margin: 0;
}
#container {
    background: #000;
    width: 100%;
    height: 100%;
    overflow: hidden;
    position: fixed;
}
canvas {
    position: absolute;
    left: 0;
    top: 0;
}
</style>
<title>amamagi</title>
</head>
<body>
    <div id="container">
        <canvas id="canvas"></canvas>
    </div>
</body></html>
