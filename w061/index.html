<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
		<title>wgld.org WebGL sample 050</title>
		<script src="https://wgld.org/j/minMatrixb.js" type="text/javascript"></script>
		<script src="https://wgld.org/j/noiseX.js" type="text/javascript"></script>
		<script src="script.js" type="text/javascript"></script>
		
		<script id="main_vs" type="x-shader/x-vertex">
// グローシェーディング
attribute vec3  position;
attribute vec3  normal;
attribute vec4  color;
uniform   mat4  mMatrix;
uniform   mat4  mvpMatrix;
uniform   mat4  invMatrix;
uniform   vec3  lightDirection;
uniform   vec3  eyePosition;
uniform   vec4  ambientColor;
varying   vec4  vColor;

void main(void){
	vec3  invLight = normalize(invMatrix * vec4(lightDirection, 0.0)).xyz;
	vec3  invEye   = normalize(invMatrix * vec4(eyePosition, 0.0)).xyz;
	vec3  halfLE   = normalize(invLight + invEye);
	float diffuse  = clamp(dot(normal, invLight), 0.1, 1.0);
	float specular = pow(clamp(dot(normal, halfLE), 0.0, 1.0), 50.0);
	vColor         = color * vec4(vec3(diffuse), 1.0) + vec4(vec3(specular), 1.0) + ambientColor;
	gl_Position    = mvpMatrix * vec4(position, 1.0);
}
		</script>
		
		<script id="main_fs" type="x-shader/x-fragment">
// グローシェーディング
precision mediump float;

varying vec4  vColor;

void main(void){
	gl_FragColor = vColor;
}
		</script>
		
		<script id="depthMap_vs" type="x-shader/x-vertex">
// 深度をオフスクリーンレンダリング
attribute vec3 position;
uniform   mat4 mvpMatrix;
varying   vec4 vPosition;

void main(void){
	vPosition   = mvpMatrix * vec4(position, 1.0);
	gl_Position = vPosition;
}
		</script>
		
		<script id="depthMap_fs" type="x-shader/x-fragment">
// 深度をオフスクリーンレンダリング
precision mediump float;

varying vec4  vPosition;

const float near = 0.1;
const float far  = 10.0;
const float linerDepth = 1.0 / (far - near);

vec4 convRGBA(float depth){
	float r = depth;
	float g = fract(r * 255.0);
	float b = fract(g * 255.0);
	float a = fract(b * 255.0);
	float coef = 1.0 / 255.0;
	r -= g * coef;
	g -= b * coef;
	b -= a * coef;
	return vec4(r, g, b, a);
}

void main(void){
	float linerPos  = linerDepth * length(vPosition);
	vec4  convColor = convRGBA(linerPos);
	gl_FragColor    = convColor;
}
		</script>
		
		<script id="fog_vs" type="x-shader/x-vertex">
// フォグを描画
attribute vec3 position;
attribute vec4 color;
attribute vec2 texCoord;
uniform   mat4 mMatrix;
uniform   mat4 mvpMatrix;
uniform   mat4 tMatrix;
varying   vec4 vPosition;
varying   vec4 vColor;
varying   vec2 vTexCoord;
varying   vec4 vTexProjCoord;

void main(void){
	vec3 pos      = (mMatrix * vec4(position, 1.0)).xyz;
	vPosition     = mvpMatrix * vec4(position, 1.0);
	vColor        = color;
	vTexCoord     = texCoord;
	vTexProjCoord = tMatrix * vec4(pos, 1.0);
	gl_Position   = vPosition;
}
		</script>
		
		<script id="fog_fs" type="x-shader/x-fragment">
// フォグを描画
precision mediump float;

uniform vec2      offset;
uniform float     distLength;
uniform sampler2D depthTexture;
uniform sampler2D noiseTexture;
uniform bool      softParticle;
varying vec4      vPosition;
varying vec4      vColor;
varying vec2      vTexCoord;
varying vec4      vTexProjCoord;

float restDepth(vec4 RGBA){
	const float rMask = 1.0;
	const float gMask = 1.0 / 255.0;
	const float bMask = 1.0 / (255.0 * 255.0);
	const float aMask = 1.0 / (255.0 * 255.0 * 255.0);
	float depth = dot(RGBA, vec4(rMask, gMask, bMask, aMask));
	return depth;
}

const float near = 0.1;
const float far  = 10.0;
const float linerDepth = 1.0 / (far - near);

void main(void){
	// RGBAに記録された深度を復元
	float depth      = restDepth(texture2DProj(depthTexture, vTexProjCoord));

	// パーティクルの深度
	float linerPos   = linerDepth * length(vPosition);
	
	// シームレスなノイズテクスチャからサンプリング 座標をずらして見た目を良くしている
	vec4  noiseColor = texture2D(noiseTexture, vTexCoord + offset);
	
	// 半月形になるよう透明度を操作
	// 底辺の中央からのlengthをとり、底辺中央を中心とした円の半径が大きくなるほど透明になり、半径0.5までより遠いとalpha=1になる 
	float alpha      = 1.0 - clamp(length(vec2(0.5, 1.0) - vTexCoord) * 2.0, 0.0, 1.0);

	if(softParticle){
		// オフスク深度とパーティクル深度の差分を取る
		float distance = abs(depth - linerPos);

		// プラマイdistLenthに収まっていたら透明にする
		if(distLength >= distance){
			float d = distance / distLength;
			alpha *= d;
		}
	}
	gl_FragColor = vec4(vColor.rgb, noiseColor.r * alpha);
}
		</script>
	</head>
	<body>
		<canvas id="canvas"></canvas>
		<p>
			<input type="checkbox" id="check" checked> soft particle fog
		</p>
		<p>
			<input type="range" id="range" min="0" max="100" value="50"> depth coef (0.0 to 0.1)
		</p>
	</body>
</html>